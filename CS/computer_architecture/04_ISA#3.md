# 컴퓨터구조

## Instruction Set Architecture(ISA) #3

* c 코드가 어셈블리 코드로 어떻게 변환되는지 확인함으로써 MIPS 명령어들이 어떤 방식으로 사용되는지 알아보자.

  

### MIPS 명령어 예제

* 산술 연산

  * c 코드

    ```c
    int f, g, h, i , j;
    f = (g+h) - (i+j);
    ```

  * 어셈블리 코드

    * 각각 변수가 $s0, $s1, $s2, $s3, $s4에 할당되었다고 가정함

    ```assembly
    add $s0, $s1, $s2	# $s0 = g + h
    add $s1, $s3, $s4	# $s1 = i + j
    sub $s0, $s0, $s1	# f = (g + h) - (i + j)
    ```

* 조건문 (if)

  * c 코드

    ```c
    if (i == j)
      i++;
    j--;
    ```

  * 어셈블리 코드

    * $s1에 i, $s2에 j가 있다고 가정함 

    ```assembly
        bne $s1, $s2, L1	# branch if !(i == j)
        addi $s1, $s1, 1	# i++
    L1: addi $s2, $s2, -1	# j--
    ```

* 조건문 (if-else)

  * c 코드

    ```c
    if (i==j)
      i++;
    else
      j--;
    j += i;
    ```

  * 어셈블리 코드

    * $s1에 i, $s2에 j가 있다고 가정함 

    ```assembly
          bne $s1, $s2, ELSE	#	branch if !(i==j)
          addi $s1, $s1, 1		# i++
          j NEXT							# jump over else			(note!)
    ELSE: addi $s2, $s2, -1		# else j--
    NEXT: add $s2, $s2, $s1		# j += i
    ```

* 혼합된 조건문

  * c 코드

    * *cond1* 은 `(i == j)` , *cond2*는` (i == k)`

    ```c
    if (<cond1> || <cond2>)
      i++;	// if-body
    else
      j--;	// else-body
    j = i + k;
    ```

  * 어셈블리 코드

    * 프로그래밍 언어 수업 시간에서 OR 조건문은 cond1이 참이면 cond2를 확인하지 않는다고 배우는데, 그 로직이 그대로 들어가 있어서 재밌다.

    ```assembly
    		beq	$s1, $s2, IF		# cond1: branch if ( i==j )
    												# 	Notice branch on TRUE
    		bne $s1, $s3, ELSE	# cond2: branch if !( i== k )
    IF:	addi $s1, $s1, 1		#	if-body:	i++
    		j NEXT							# jump over else
    ELSE: addi $s2 $s2, -1 	# else-body: j--
    NEXT: add $s2, $s1, $s3	# j = i + k
    ```

* 조건문 (switch)

  * c 코드

    ```c
    switch(i) {
      case 1: i++;
      case 2: i += 2;
        break;
      case 3: i += 3;
    }
    ```

  * 어셈블리 코드

    * $s1에는 i, $s4에는  temp(임의의 값)가 들어있다고 가정한다.
    * 여기서 $zero가 쓰였는데, 0~31번 레지스터 중 0번째 레지스터란 뜻으로 항상 0이 들어가 있음. 각각 레지스터는 역할이 정해져 있음 
      * 예컨대 지난 문서 JAL 명령어에서 언급했듯 31번 레지스터는 함수콜 됐을 때 돌아갈 주소

    ```assembly
    					addi $s4, $zero, 1			# case 1: set temp 1
    																	# $zero 와 addi 는 위와 같은 방식으로 요긴하게 쓰임
    					bne  $s1, $s4, C2_COND	#		false: branch to case 2 cond
    					j C1_BODY								#		true: branch to case 1 body
    C2_COND:	addi $s4, $zero, 2			# case 2: set temp to 2
    					bne $s1, $s4, C3_COND		#		false: branch to case 3 cond
    					j C2_BODY								#		true: branch to case 2 body
    C3_COND:	addi $s4, $zero, 3			# case 3: set temp to 3
    					bne $s1, $s4, EXIT			#		false: branch to exit
    					j C3_BODY								#		true: branch to case 3 body
    C1_BODY:	addi $s1, $s1, 1				# case 1 body: i ++
    C2_BODY:	addi $s1, $s1, 2				# case 2 body: i += 2
    					j EXIT									# break! 
    C3_BODY:	addi $s1, $s1, 3				# case 3 body: i += 3
    EXIT:
    ```

* 반복문 (while)

  * c 코드

    ```c
    while (i < j){
      k++;
      i = i*2;
    }
    ```

  * 어셈블리 코드 

    * $s1에 i, $s2에  j, $s3에  k가 들어있다 가정

    ```assembly
    L1:		bge  $s1, $s2, DONE		# branch if !( i<j ) 				안 맞으면(i>=j) 바로 루프 탈출 
    			addi $s3, $s3, 1			# 	k++	
    			add  $s1, $s1, $s1		#		i = i*2									
    														# 덧셈으로 곱하기를 표현했음. 보통 곱셈이 덧셈보다 연산 시간이 더 걸리기 때문. shift left로 표현해서 더 성능을 높일 수도 있을 것임.
    			j    L1								# jump back to top of loop
    DONE:
    ```

* 반복문 (for)

  * c 코드

    ```c
    for (<init>; <cond>; <update>){
      <for-body>
    }
    ```

  * 어셈블리 코드

    * init 이 추가된거 말고는  while과 동일함. 아래는 수도코드

    ```assembly
    <init>;
    L1:		<cond> {
            <for-body>
            <update>
            goto L1;
    			}
    DONE:
    ```

    

